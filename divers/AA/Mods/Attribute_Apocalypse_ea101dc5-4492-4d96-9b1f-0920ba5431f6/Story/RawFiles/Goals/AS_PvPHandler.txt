Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_AS_PVP_LEVELUP(5);
DB_AS_PVP_LEVELUP(9);
DB_AS_PVP_LEVELUP(13);
DB_AS_PVP_LEVELUP(16);
DB_AS_PVP_LEVELUP(18);
KBSECTION
IF
CharacterLeveledUp(_Char)
AND
CharacterIsPlayer(_Char,1)
AND
CharacterGetLevel(_Char,_Lvl)
AND
DB_AS_PVP_LEVELUP(_Lvl)
THEN
DB_AS_PVP_DOLEVEL(_Lvl);
IterateParties("AS_PVPDOLEVELUP");
TimerLaunch("AS_ClearPvpLvlup",100);

IF
StoryEvent((CHARACTERGUID)_Char,"AS_PVPDOLEVELUP")
AND
DB_AS_PVP_DOLEVEL(_Lvl)
AND
CharacterIsPlayer(_Char,1)	// This is redundant, but I'm seriously paranoid.
THEN
CharacterLevelUpTo(_Char,_Lvl);

IF
TimerFinished("AS_ClearPvpLvlup")
AND
DB_AS_PVP_DOLEVEL(_Lvl)
THEN
NOT DB_AS_PVP_DOLEVEL(_Lvl);


IF
RegionEnded(_Any)
THEN
IterateParties("AS_ClearUpSpawnpoints");

IF
StoryEvent((CHARACTERGUID)_Char,"AS_ClearUpSpawnpoints")
AND
DB_AS_PVP_LastTrigger(_Char,_X,_Y,_Z)
THEN
NOT DB_AS_PVP_LastTrigger(_Char,_X,_Y,_Z);

IF
RegionStarted(_Any)
THEN
IterateParties("AS_SetUpSpawnpoints");

IF
StoryEvent((CHARACTERGUID)_Char,"AS_SetUpSpawnpoints")
AND
NOT DB_AS_PVP_LastTrigger(_Char,_,_,_)
AND
GetPosition(_Char,_X,_Y,_Z)
THEN
DB_AS_PVP_LastTrigger(_Char,_X,_Y,_Z);

IF
CharacterTeleportToWaypoint(_Char,_Trigger)
AND
DB_AS_PVP_LastTrigger(_Char,_A,_B,_C)
THEN
NOT DB_AS_PVP_LastTrigger(_Char,_A,_B,_C);

IF
CharacterTeleportToWaypoint(_Char,_Trigger)
AND
GetPosition(_Char,_X,_Y,_Z)
THEN
DB_AS_PVP_LastTrigger(_Char,_X,_Y,_Z);


IF
CharacterLeftParty((CHARACTERGUID)_Char)
AND
CharacterIsInCombat(_Char,1)
THEN
ObjectSetFlag(_Char,"AS_PVP_ResurrectBlacklist");

IF
ObjectLeftCombat(_Char,_)
THEN
ProcObjectTimer(_Char,"AS_PVP_ClearRessBlacklist",2000);

PROC
ProcObjectTimerFinished(_Char,"AS_PVP_ClearRessBlacklist")
THEN
ObjectClearFlag(_Char,"AS_PVP_ResurrectBlacklist");

IF
CharacterKilledBy(_Player,_,_Attacker)
AND
DB_CurrentGameMode(_String)
AND
_String != "GameMaster"
AND
GetRegion(_Player,_Region)
AND
_Region != "Tomb of Lucian"
AND
HasActiveStatus(_Player,"AS_ResurrectionSickness",0)
AND
CharacterIsPlayer(_Player,1)
AND
CharacterIsPlayer(_Attacker,1)
AND
CharacterIsInPartyWith(_Player,_Attacker,0)
AND
ObjectGetFlag(_Player,"AS_PVP_ResurrectBlacklist",0)
AND
ObjectGetFlag(_Attacker,"AS_PVP_ResurrectBlacklist",0)
AND
IsTagged(_Player,"BLOCK_RESURRECTION",0)
AND
ItemTemplateIsInCharacterInventory(_Player,"Quest_Resurrection_Idol_6b70e6e1-855e-4295-b79b-d87ebd6bd4af",_Amount)
AND
_Amount < 1
THEN
ProcObjectTimer(_Player,"AS_PVP_Resurrect",2000);

IF
CharacterKilledBy(_Player,_Attacker,_)
AND
DB_CurrentGameMode(_String)
AND
_String != "GameMaster"
AND
GetRegion(_Player,_Region)
AND
_Region != "Tomb of Lucian"
AND
ObjectGetFlag(_Player,"AS_PvP_CannotRess",0)		// Check they didn't have Ress Sickness
AND
CharacterIsPlayer(_Player,1)
AND
CharacterIsPlayer(_Attacker,1)
AND
CharacterIsInPartyWith(_Player,_Attacker,0)
AND
ObjectGetFlag(_Player,"AS_PVP_ResurrectBlacklist",0)
AND
ObjectGetFlag(_Attacker,"AS_PVP_ResurrectBlacklist",0)
AND
IsTagged(_Player,"BLOCK_RESURRECTION",0)
AND
ItemTemplateIsInCharacterInventory(_Player,"Quest_Resurrection_Idol_6b70e6e1-855e-4295-b79b-d87ebd6bd4af",_Amount)
AND
_Amount < 1
THEN
ProcObjectTimer(_Player,"AS_PVP_Resurrect",2000);


PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char,"AS_PVP_Resurrect")
AND
DB_AS_PVP_LastTrigger(_Char,_X,_Y,_Z)
THEN
TeleportToPosition(_Char,_X,_Y,_Z,"AS_TeleportResurrect",0,1);
CharacterResurrect(_Char);
ProcObjectTimer(_Char,"AS_ApplyRessSick",1000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char,"AS_ApplyRessSick")
AND
CharacterHasTalent(_Char,"ResurrectToFullHealth",0)
THEN
ApplyStatus(_Char,"AS_RESSSICK",180.0,1);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char,"AS_ApplyRessSick")
AND
CharacterHasTalent(_Char,"ResurrectToFullHealth",1)
THEN
ApplyStatus(_Char,"AS_RESSSICK2",180.0,1);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Player,"AS_ApplyRessSick")
THEN
Proc_StartDialog(1,"FTJ_VB_FallIntoPrison",_Player);



IF
TextEventSet("AS_TESTRESS")
THEN
IterateParties("AS_PROCRESS");

IF
StoryEvent(_Char,"AS_PROCRESS")
THEN
ProcObjectTimer(_Char,"AS_ApplyRessSick",2000);


IF
CharacterStatusApplied(_Char,"AS_RESSSICK",_)
THEN
ObjectSetFlag(_Char,"AS_PvP_CannotRess");

IF
CharacterStatusRemoved(_Char,"AS_RESSSICK",_)
THEN
ProcObjectTimer(_Char,"AS_PvP_ClearCannotRess",12000);

PROC
ProcObjectTimerFinished((CHARACTERGUID)_Char,"AS_PvP_ClearCannotRess")
THEN
ObjectClearFlag(_Char,"AS_PvP_CannotRess");
EXITSECTION

ENDEXITSECTION
